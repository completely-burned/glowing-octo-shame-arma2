/* функция удаляет группу должным образом
 * колличество групп ограниченно движком для каждой стороны
 * например уничноженные не локальные ии занимают группу
 * максимальное количество юнитов в группе мне не изместно
 *
 * эта функция нужна, т.к. в данный момент
 * группы удаляются только на сервере
 * а удаление групп клиента может быть нарушено
 *
 * TODO: нужно удалять группы так-же у клиентов
 * т.к. перемещение юнитов в другую группу
 * может вызывать синхронизацию мультиплеера
 * и вызывать падение производительности :TODO
 */

private["_u"];
_u = units _this;

if (_this != deadGroup && {_x call gosa_fnc_isPlayer} count _u == 0) then { // специальные группы удалять не нужно

	diag_log format ["Log: [fnc_deleteGroup.sqf] deleteGroup %1, units %2", _this, _u];

	// иногда специальная группа удаляется по неизвестной причине
	if (isNull deadGroup) then {
		deadGroup = createGroup civilian;
	};

	// перемещаем уничтоженных юнитов этой группы в специальную группу
	_u joinSilent deadGroup;

	// юниты перемещаются не сразу, и группа скорее всего не удалиться первой попытки, если не пуста
	deleteGroup _this;
}else{
    diag_log format ["Log: [fnc_deleteGroup.sqf] not deleteGroup %1, units %2", _this, _u];
};
